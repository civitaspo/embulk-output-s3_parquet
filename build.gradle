plugins {
    id "scala"
    id "com.jfrog.bintray" version "1.1"
    id "com.github.jruby-gradle.base" version "1.5.0"
    id "com.adarshr.test-logger" version "1.6.0"  // For Pretty test logging
}
import com.github.jrubygradle.JRubyExec
repositories {
    mavenCentral()
    jcenter()
}
configurations {
    provided
}

version = "0.0.2"

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compile  "org.embulk:embulk-core:0.9.17"
    provided "org.embulk:embulk-core:0.9.17"

    compile 'org.scala-lang:scala-library:2.12.8'
    ['s3', 'sts'].each { v ->
        compile "com.amazonaws:aws-java-sdk-${v}:1.11.479"
    }
    ['column', 'common', 'encoding', 'format', 'hadoop', 'jackson'].each { v ->
        compile "org.apache.parquet:parquet-${v}:1.10.0"
    }
    compile 'org.apache.hadoop:hadoop-common:2.9.2'
    compile 'org.xerial.snappy:snappy-java:1.1.7.3'

    testCompile 'org.scalatest:scalatest_2.12:3.0.5'
    testCompile 'org.embulk:embulk-test:0.9.17'
    testCompile 'org.embulk:embulk-standards:0.9.17'
    testCompile 'cloud.localstack:localstack-utils:0.1.15'
    testCompile 'org.apache.parquet:parquet-tools:1.8.0'
    testCompile 'org.apache.hadoop:hadoop-client:2.9.2'
}

testlogger {
    theme "mocha"
}

task classpath(type: Copy, dependsOn: ["jar"]) {
    doFirst { file("classpath").deleteDir() }
    from (configurations.runtime - configurations.provided + files(jar.archivePath))
    into "classpath"
}
clean { delete "classpath" }

task gem(type: JRubyExec, dependsOn: ["gemspec", "classpath"]) {
    jrubyArgs "-S"
    script "gem"
    scriptArgs "build", "${project.name}.gemspec"
    doLast { ant.move(file: "${project.name}-${project.version}.gem", todir: "pkg") }
}

task gemPush(type: JRubyExec, dependsOn: ["gem"]) {
    jrubyArgs "-S"
    script "gem"
    scriptArgs "push", "pkg/${project.name}-${project.version}.gem"
}

task "package"(dependsOn: ["gemspec", "classpath"]) {
    doLast {
        println "> Build succeeded."
        println "> You can run embulk with '-L ${file(".").absolutePath}' argument."
    }
}

task gemspec {
    ext.gemspecFile = file("${project.name}.gemspec")
    inputs.file "build.gradle"
    outputs.file gemspecFile
    doLast { gemspecFile.write($/
Gem::Specification.new do |spec|
  spec.name          = "${project.name}"
  spec.version       = "${project.version}"
  spec.authors       = ["Civitaspo"]
  spec.summary       = %[S3 Parquet output plugin for Embulk]
  spec.description   = %[Dumps records to S3 Parquet.]
  spec.email         = ["civitaspo@gmail.com"]
  spec.licenses      = ["MIT"]
  spec.homepage      = "https://github.com/civitaspo/embulk-output-s3_parquet"

  spec.files         = `git ls-files`.split("\n") + Dir["classpath/*.jar"]
  spec.test_files    = spec.files.grep(%r"^(test|spec)/")
  spec.require_paths = ["lib"]

  #spec.add_dependency 'YOUR_GEM_DEPENDENCY', ['~> YOUR_GEM_DEPENDENCY_VERSION']
  spec.add_development_dependency 'bundler', ['~> 1.0']
  spec.add_development_dependency 'rake', ['~> 12.0']
end
/$)
    }
}
clean { delete "${project.name}.gemspec" }
